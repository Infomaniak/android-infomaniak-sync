/*
 * Copyright (c) 2013 â€“ 2016 Ricki Hirner (bitfire web engineering).
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the GNU Public License v3.0
 * which accompanies this distribution, and is available at
 * http://www.gnu.org/licenses/gpl.html
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.3'

    defaultConfig {
        applicationId "at.bitfire.davdroid"
        resValue "string", "packageID", applicationId

        versionCode 120
        buildConfigField "long", "buildTime", System.currentTimeMillis() + "L"

        minSdkVersion 15
        targetSdkVersion 24

        buildConfigField "boolean", "customCerts", "false"
        buildConfigField "at.bitfire.vcard4android.GroupMethod", "settingContactGroupMethod", "null"
        buildConfigField "Boolean", "settingVCardRFC6868", "null"
    }

    productFlavors {
        standard {
            versionName "1.3.2.2"
            buildConfigField "boolean", "customCerts", "true"
        }
        gplay {
            versionName "1.3.2.2-gplay"
            buildConfigField "boolean", "customCerts", "true"
        }
        icloud {
            applicationId "at.bitfire.cloudsync"
            resValue "string", "packageID", applicationId

            versionName "1.3.2.2-cloud"

            buildConfigField "at.bitfire.vcard4android.GroupMethod", "settingContactGroupMethod", "at.bitfire.vcard4android.GroupMethod.GROUP_VCARDS"
            buildConfigField "Boolean", "settingVCardRFC6868", "true"
        }
    }

    sourceSets {
        standard.java.srcDirs = [ "src/davdroid/java" ]
        standard.res.srcDirs = [ "src/davdroid/res" ]

        gplay.java.srcDirs = [ "src/gplay/java", "src/davdroid/java" ]
        gplay.res.srcDirs = [ "src/gplay/res", "src/davdroid/res" ]
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'      // we don't need Google indexing, thanks
        disable 'GradleDependency'
        disable 'GradleDynamicVersion'
        disable 'IconColors'
        disable 'IconLauncherShape'
        disable 'IconMissingDensityFolder'
        disable 'ImpliedQuantity'
        disable 'MissingTranslation'
        disable 'MissingQuantity'
        disable 'Recycle'           // doesn't understand Lombok's @Cleanup
        disable 'RtlEnabled'
        disable 'RtlHardcoded'
        disable 'Typos'
    }
    packagingOptions {
        exclude 'LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}

dependencies {
    compile project(':cert4android')
    compile project(':dav4android')
    compile project(':ical4android')
    compile project(':vcard4android')

    compile 'com.android.support:appcompat-v7:24.+'
    compile 'com.android.support:cardview-v7:24.+'
    compile 'com.android.support:design:24.+'
    compile 'com.android.support:preference-v14:24.+'

    compile 'com.github.yukuku:ambilwarna:2.0.1'

    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'dnsjava:dnsjava:2.1.7'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-collections4:4.1'
    provided 'org.projectlombok:lombok:1.16.10'

    // for tests
    androidTestCompile('com.android.support.test:runner:0.5') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.5') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.4.1'

    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.4.1'
}
