/*
 * Copyright (c) Ricki Hirner (bitfire web engineering).
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the GNU Public License v3.0
 * which accompanies this distribution, and is available at
 * http://www.gnu.org/licenses/gpl.html
 */

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "at.bitfire.davdroid"
        resValue "string", "packageID", applicationId

        versionCode 151
        buildConfigField "long", "buildTime", System.currentTimeMillis() + "L"

        minSdkVersion 15
        targetSdkVersion 25

        buildConfigField "boolean", "customCerts", "false"
        buildConfigField "at.bitfire.vcard4android.GroupMethod", "settingContactGroupMethod", "null"
    }

    productFlavors {
        standard {
            versionName "1.6.1.1"
            buildConfigField "boolean", "customCerts", "true"
        }

        gplay {
            versionName "1.6.1.1-gplay"
            buildConfigField "boolean", "customCerts", "true"
        }
        icloud {
            applicationId "at.bitfire.cloudsync"
            resValue "string", "packageID", applicationId

            versionName "1.6.1.1-cloud"
            buildConfigField "at.bitfire.vcard4android.GroupMethod", "settingContactGroupMethod", "at.bitfire.vcard4android.GroupMethod.GROUP_VCARDS"
        }
        soldupe {
            applicationId "com.soldupe.cloudsync"
            resValue "string", "packageID", applicationId
            minSdkVersion 21

            versionName "1.6.1.1-soldupe"
            buildConfigField "boolean", "customCerts", "true"
            buildConfigField "at.bitfire.vcard4android.GroupMethod", "settingContactGroupMethod", "at.bitfire.vcard4android.GroupMethod.CATEGORIES"
        }
    }

    sourceSets {
        standard.java.srcDirs = [ "src/davdroid/java" ]
        standard.res.srcDirs = [ "src/davdroid/res" ]
        gplay.java.srcDirs = [ "src/gplay/java", "src/davdroid/java" ]
        gplay.res.srcDirs = [ "src/gplay/res", "src/davdroid/res" ]

    }

    signingConfigs {
        bitfire {
            storeFile file("${System.env.HOME}/Entwicklung/GooglePlay/bitfire.jks")
            storePassword 'futfut12'
            keyAlias 'bitfire'
            keyPassword 'futfut12'
        }
        soldupe {
            storeFile file("${System.env.HOME}/Entwicklung/GooglePlay/soldupe.jks")
            storePassword 'hei8eePh'
            keyAlias 'soldupe'
            keyPassword 'ocaip6oZ'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            signingConfig signingConfigs.bitfire
            productFlavors.soldupe.signingConfig signingConfigs.soldupe
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'      // we don't need Google indexing, thanks
        disable 'GradleDependency'
        disable 'GradleDynamicVersion'
        disable 'IconColors'
        disable 'IconLauncherShape'
        disable 'IconMissingDensityFolder'
        disable 'ImpliedQuantity', 'MissingQuantity'
        disable 'MissingTranslation', 'ExtraTranslation'	// translations from Transifex are not always up to date
        disable 'Recycle'           // doesn't understand Lombok's @Cleanup
        disable 'RtlEnabled'
        disable 'RtlHardcoded'
        disable 'Typos'
    }
    packagingOptions {
        exclude 'LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}

dependencies {
    compile project(':cert4android')
    compile project(':dav4android')
    compile project(':ical4android')
    compile project(':vcard4android')

    compile 'com.android.support:appcompat-v7:25.+'
    compile 'com.android.support:cardview-v7:25.+'
    compile 'com.android.support:design:25.+'
    compile 'com.android.support:preference-v14:25.+'

    compile 'com.github.yukuku:ambilwarna:2.0.1'

    compile 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    compile 'commons-io:commons-io:2.5'
    compile 'dnsjava:dnsjava:2.1.8'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'org.apache.commons:commons-collections4:4.1'
    provided 'org.projectlombok:lombok:1.16.16'

    // for tests
    androidTestCompile('com.android.support.test:runner:+') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:+') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.8.1'

    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.8.1'
}
