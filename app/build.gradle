/*
 * Copyright (c) Ricki Hirner (bitfire web engineering).
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the GNU Public License v3.0
 * which accompanies this distribution, and is available at
 * http://www.gnu.org/licenses/gpl.html
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "at.bitfire.davdroid"
        resValue "string", "packageID", applicationId

        versionCode 211
        buildConfigField "long", "buildTime", System.currentTimeMillis() + "L"

        minSdkVersion 19        // Android 4.4
        targetSdkVersion 27     // Android 8.1

        buildConfigField "boolean", "customCerts", "false"
        buildConfigField "boolean", "customCertsUI", "true"

        // when using this, make sure that notification icons are real bitmaps
        vectorDrawables.useSupportLibrary = true
    }

    flavorDimensions "type"

    productFlavors {
        standard {
            dimension "type"
            versionName "1.11-rc1"

            buildConfigField "boolean", "customCerts", "true"
        }
        managed {
            dimension "type"
            versionName "1.11-rc1-mgd"

            applicationId "com.davdroid.managed"
            resValue "string", "packageID", applicationId

            buildConfigField "boolean", "customCerts", "true"
            buildConfigField "boolean", "customCertsUI", "false"
            minSdkVersion 21
        }

        gplay {
            dimension "type"
            versionName "1.11-rc1-gplay"

            buildConfigField "boolean", "customCerts", "true"
        }
        icloud {
            dimension "type"
            versionName "1.11-rc1-cloud"

            applicationId "at.bitfire.cloudsync"
            resValue "string", "packageID", applicationId
        }
        soldupe {
            dimension "type"
            versionName "1.11-rc1-soldupe"

            applicationId "com.soldupe.cloudsync"
            resValue "string", "packageID", applicationId
            minSdkVersion 21
        }
    }

    sourceSets {
        standard.java.srcDirs = [ "src/davdroid/java" ]
        standard.res.srcDirs = [ "src/davdroid/res" ]
        gplay.java.srcDirs = [ "src/gplay/java", "src/davdroid/java" ]
        gplay.res.srcDirs = [ "src/gplay/res", "src/davdroid/res" ]
        androidTest.java.srcDirs = [ "src/androidTest/java", "src/espressoTest/java" ]
    }

    signingConfigs {
        bitfire {
            storeFile file("${System.env.HOME}/Entwicklung/GooglePlay/bitfire.jks")
            storePassword 'futfut12'
            keyAlias 'bitfire'
            keyPassword 'futfut12'
        }
        soldupe {
            storeFile file("${System.env.HOME}/Entwicklung/GooglePlay/soldupe.jks")
            storePassword 'hei8eePh'
            keyAlias 'soldupe'
            keyPassword 'ocaip6oZ'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            signingConfig signingConfigs.bitfire
            productFlavors.soldupe.signingConfig signingConfigs.soldupe
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'      // we don't need Google indexing, thanks
        disable 'ImpliedQuantity', 'MissingQuantity'		// quantities from Transifex may vary
        disable 'MissingTranslation', 'ExtraTranslation'	// translations from Transifex are not always up to date
        disable "OnClick"     // doesn't recognize Kotlin onClick methods
        disable 'RtlEnabled'
        disable 'RtlHardcoded'
        disable 'Typos'
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
    }

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}

dependencies {
    compile project(':cert4android')
    compile project(':dav4android')
    compile project(':ical4android')
    compile project(':vcard4android')

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    compile 'com.android.support:appcompat-v7:27.1.0'
    compile 'com.android.support:cardview-v7:27.1.0'
    compile 'com.android.support:design:27.1.0'
    compile 'com.android.support:preference-v14:27.1.0'
    compile 'com.android.support.test.espresso:espresso-idling-resource:3.0.1'

    compile 'com.github.yukuku:ambilwarna:2.0.1'

    compile 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    compile 'commons-io:commons-io:2.6'
    compile 'dnsjava:dnsjava:2.1.8'
    compile 'org.apache.commons:commons-lang3:3.6'
    compile 'org.apache.commons:commons-collections4:4.1'

    // for tests
    androidTestCompile 'com.android.support.test:runner:1.0.1'
    androidTestCompile 'com.android.support.test:rules:1.0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:3.0.1'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.10.0'

    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.10.0'
}
